apply plugin: "java-library"
apply plugin: "maven-publish"

// Plugin config
compileJava.options.encoding = 'UTF-8'
ext.pluginNameUpper = "NyaaUtils"
ext.pluginNameLower = ext.pluginNameUpper.toLowerCase()
ext.majorVersion = 8
ext.minorVersion = 0
ext.minecraftVersion = "1.16.2"
sourceCompatibility = 1.8
targetCompatibility = 1.8


// Suppiled by Jenkins
ext.buildNumber = System.env.BUILD_NUMBER == null ? "x"                : "$System.env.BUILD_NUMBER"
ext.mavenDirectory = System.env.MAVEN_DIR == null ? "$projectDir/repo" : "$System.env.MAVEN_DIR"
ext.jdDirectory = System.env.JAVADOCS_DIR == null ? null               : "$System.env.JAVADOCS_DIR"

// Search for spigot nms jar file
String spigotNmsPath = "";
if (System.env.NMS_JAR != null) {
    // use NMS_JAR if it's explicitly specified
    spigotNmsPath = "$System.env.NMS_JAR"
    if (new File(spigotNmsPath).exists()) {
        logger.warn("NMS jar is set manually: ${spigotNmsPath}")
    } else {
        throw new GradleException("NMS jar not found: ${spigotNmsPath}")
    }
} else if (new File("${mavenDirectory}/spigot-${minecraftVersion}-latest.jar").exists()) {
    // ci environment
    spigotNmsPath = "${mavenDirectory}/spigot-${minecraftVersion}-latest.jar"
} else {
    // check local dir (dev environment)
    spigotNmsPath = "${projectDir}/../nms_binaries/spigot-${minecraftVersion}.jar"
    if (!(new File(spigotNmsPath).exists())) {
        // nms not found, download from nyaaci
        def f = new File(spigotNmsPath)
        println "Downloading spigot-${minecraftVersion}.jar"
        f.getParentFile().mkdirs();
        new URL("https://ci.nyaacat.com/maven/spigot-${minecraftVersion}-latest.jar").withInputStream{ i -> f.withOutputStream{ it << i }}
    }
}
println ("Found NMS jar: ${spigotNmsPath}")

// Version used for distribution. Different from maven repo
group = "cat.nyaa"
archivesBaseName = "${pluginNameUpper}-mc$minecraftVersion"
version = "$majorVersion.$minorVersion.$buildNumber"

repositories {
    jcenter()
    maven { name 'Spigot';      url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { name 'Sonatype';    url 'https://oss.sonatype.org/content/groups/public' }
    maven { name 'sk89q';       url 'http://maven.sk89q.com/artifactory/repo/' }
    maven { name 'vault-repo';  url 'https://jitpack.io' }
    maven { name 'NyaaCat';     url 'https://ci.nyaacat.com/maven/' }
    maven { name 'EssentialsX'; url 'https://ci.ender.zone/plugin/repository/everything' }
    maven { name 'aikar';       url 'https://repo.aikar.co/content/groups/aikar/' }
}

dependencies {
    // spigot dependencies
    compileOnly "org.spigotmc:spigot-api:$minecraftVersion-R0.1-SNAPSHOT"
    compileOnly files(spigotNmsPath)
    // Nyaa plugins
    if (gradle.hasProperty("useLocalDependencies") && gradle.useLocalDependencies) {
        compileOnly project(":NyaaCore")
        compileOnly project(":LockettePro")
    } else {
        compileOnly "cat.nyaa:nyaacore:${majorVersion}.${minorVersion}-SNAPSHOT"
        compileOnly "me.crafter.mc:lockettepro:2.9-SNAPSHOT"
    }

    // Other plugins
    compileOnly('net.ess3:EssentialsX:2.18.0') { transitive = false }
    compileOnly ('com.sk89q.worldedit:worldedit-bukkit:7.1.0-SNAPSHOT') {
        exclude group: 'io.papermc', module: 'paperlib'
        exclude group: 'org.bstats.bStats-Metrics', module: 'bstats-bukkit'
        exclude group: 'org.bukkit', module: 'bukkit'
        exclude group: 'com.destroystokyo.paper', module: 'paper-api'
    }
}

// source file modification (modify version string)
processResources {
    filesMatching("**/plugin.yml") {
        expand 'version': project.version
    }
}

// source file jar
task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

// javadoc generation options
javadoc {
    // javadoc output folder
    if (project.jdDirectory != null) destinationDir = file("${jdDirectory}/${pluginNameLower}-${version}")

    options {
        links 'https://docs.oracle.com/en/java/javase/14/docs/api/'
        links 'https://hub.spigotmc.org/javadocs/spigot/'
        links 'https://google.github.io/guava/releases/21.0/api/docs/'
        links 'https://ci.md-5.net/job/BungeeCord/ws/chat/target/apidocs/'

        locale 'en_US'
        encoding 'UTF-8'
        docEncoding 'UTF-8'
        addBooleanOption('keywords', true)
        addStringOption('Xdoclint:none', '-quiet')

        if (JavaVersion.current() > JavaVersion.VERSION_1_9) {
            addBooleanOption('html5', true)
        }

        windowTitle = "${pluginNameUpper} Javadoc"
        docTitle = "${pluginNameUpper} (mc$minecraftVersion-${project.version})"
    }
}

// javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

// compile options
compileJava {
    options.compilerArgs += ["-Xlint:deprecation"]
}

// maven publish
publishing {
    publications {
        mavenJava(MavenPublication) {
            group      project.group
            artifactId pluginNameLower
            version    "$majorVersion.$minorVersion-SNAPSHOT"

            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url mavenDirectory
        }
    }
}
